plugins {
    id 'com.moowork.node' version "1.2.0"
}

apply plugin: 'kotlin-platform-js'
apply plugin: 'kotlin-dce-js'
apply plugin: 'kotlin2js'

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-js:${version_kotlin}"
    expectedBy project(":processor")

    testImplementation "org.jetbrains.kotlin:kotlin-test-js:${version_kotlin}"
    
}


node {
    download = true
}

compileKotlin2Js {
    kotlinOptions.moduleKind = "umd"
}

compileTestKotlin2Js {
    kotlinOptions.moduleKind = "umd"
}


task populateNodeModules(type: Copy, dependsOn: compileKotlin2Js) {
    from compileKotlin2Js.destinationDir

    configurations.testRuntimeClasspath.each {
        from zipTree(it.absolutePath).matching { include '*.js' }
    }

    into "${buildDir}/node_modules"
}

task installMocha(type: YarnTask) {
    args = ['add', 'mocha']
}

task runMocha(type: NodeTask, dependsOn: [compileTestKotlin2Js, populateNodeModules, installMocha]) {
    script = file('node_modules/mocha/bin/mocha')
    args = [compileTestKotlin2Js.outputFile]
}

test.dependsOn runMocha